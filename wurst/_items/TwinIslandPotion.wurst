package TwinIslandPotion
import ClosureEvents
import AbilityObjEditing
import DummyCaster
import Orders

@compiletime function createVisionSpell()
    new AbilityDefinitionFaerieFirecreep('XTWN')
    ..setDurationHero(1, 30)
    ..setDurationNormal(1, 30)
    ..setBuffs(1, "")
    ..setDefenseReduction(1, 0)
    ..setHeroAbility(false)
    ..setItemAbility(false)
    ..setCastRange(1, 90000)
    ..setCastingTime(1, 0)
    ..setArtEffect("")
    ..setArtCaster("")
    ..setArtTarget("")
    ..setCooldown(1, 0)

function onCast()
    var caster = GetSpellAbilityUnit()
    var owner = caster.getOwner()

    for u in udg_trolls
        if u.getOwner().isEnemyOf(owner)
            new DummyCaster().castTarget(owner, 'XTWN', 1, Orders.faeriefire, u)
            PingMinimapForForceEx(GetPlayersAllies(owner), u.getPos().x, u.getPos().y, 8, bj_MINIMAPPINGSTYLE_ATTACK, 255, 0, 0)

init
    registerSpellEffectEvent('A03F', () -> onCast())