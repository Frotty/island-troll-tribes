package ShapeshiftRework

import AbilityObjEditing
import ObjectIdGenerator
import ID
import ClosureEvents
import HashMap
import ObjectIds
import MeatSystem
import SimError

let FORM_IDS = [UNIT_SHAPESHIFTER_WOLF, UNIT_SHAPESHIFTER_BEAR, UNIT_SHAPESHIFTER_PANTHER, UNIT_SHAPESHIFTER_TIGER]
let FORM_MAP = new HashMap<string, integer>()
let TRANSFORM_ABILITY_IDS = [compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()),
compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()),
compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next()), compiletime(ABIL_ID_GEN.next())]
public let ABILITY_ID_VICIOUS_STRIKES1 = 'A0GG'
public let ABILITY_ID_VICIOUS_STRIKES2 = 'A0GH'
public let ABILITY_ID_BEARS_BULWARK = 'A0GE'
public let ABILITY_ID_WOLFS_HUNGER = 'A0GD'
public let ABILITY_ID_PROWL = 'A0GF'
let ABILITY_ID_WOLF_FORM_DUMMY = 'A00H'
let ABILITY_ID_BEAR_FORM_DUMMY = 'A0GN'
let ABILITY_ID_PANTHER_FORM_DUMMY = 'A0GO'
let ABILITY_ID_TIGER_FORM_DUMMY = 'A0GP'
let TRANSFORM_FX_PATH = "Abilities\\Spells\\Human\\Polymorph\\PolyMorphDoneGround.mdl"

@compiletime function formChanger()
    var i = 0
    for oldForm = 0 to 3
        for newForm = 0 to 3
            if newForm != oldForm
                new AbilityDefinitionBearform(TRANSFORM_ABILITY_IDS[i])
                ..setName(FORM_IDS[oldForm].toRawCode() + " to " + FORM_IDS[newForm].toRawCode())
                ..presetAlternateFormUnit(lvl -> FORM_IDS[oldForm].toRawCode())
                ..presetNormalFormUnit(lvl -> FORM_IDS[newForm].toRawCode())
                i++

function onCast(int newFormId)
    var caster = GetSpellAbilityUnit()
    var owner = caster.getOwner()
    var oldF = caster.getTypeId()
    var changeKey = oldF.toRawCode() + newFormId.toRawCode()
    if not FORM_MAP.has(changeKey)
        simError(owner, "You already are in that form.")
        return

    //Make the form abilities in the spellbook permanent so they wont disappear
    caster.makeAbilityPermanent('A00H', true)
    caster.makeAbilityPermanent('A0GN', true)
    caster.makeAbilityPermanent('A0GO', true)
    caster.makeAbilityPermanent('A0GP', true)

    //Add / remove bear form ability to transform unit
    var changeAbility = FORM_MAP.get(changeKey)
    caster.addAbility(changeAbility)
    caster.removeAbility(changeAbility)
    let FX = caster.addEffect(TRANSFORM_FX_PATH, "origin")
    FX.setScale(2)
    FX.destr()

    updateNewFormAbilities(owner, newFormId)

    //Make sure new form has meat abilities
    if meatInventoryInstances.has(caster)
        meatInventoryInstances.get(caster).updateMeatAbilities()

function updateNewFormAbilities(player owner, int newFormId)
    switch newFormId
        case FORM_IDS[0]
            owner.setAbilityAvailable(ABILITY_ID_WOLFS_HUNGER, true)
            owner.setAbilityAvailable(ABILITY_ID_BEARS_BULWARK, false)
            owner.setAbilityAvailable(ABILITY_ID_PROWL, false)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES1, false)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES2, false)
        case FORM_IDS[1]
            owner.setAbilityAvailable(ABILITY_ID_WOLFS_HUNGER, false)
            owner.setAbilityAvailable(ABILITY_ID_BEARS_BULWARK, true)
            owner.setAbilityAvailable(ABILITY_ID_PROWL, false)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES1, false)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES2, false)
        case FORM_IDS[2]
            owner.setAbilityAvailable(ABILITY_ID_WOLFS_HUNGER, false)
            owner.setAbilityAvailable(ABILITY_ID_BEARS_BULWARK, false)
            owner.setAbilityAvailable(ABILITY_ID_PROWL, true)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES1, false)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES2, false)
        case FORM_IDS[3]
            owner.setAbilityAvailable(ABILITY_ID_WOLFS_HUNGER, false)
            owner.setAbilityAvailable(ABILITY_ID_BEARS_BULWARK, false)
            owner.setAbilityAvailable(ABILITY_ID_PROWL, false)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES1, true)
            owner.setAbilityAvailable(ABILITY_ID_VICIOUS_STRIKES2, true)

init
    registerSpellEffectEvent(ABILITY_ID_WOLF_FORM_DUMMY, () -> onCast(FORM_IDS[0]))
    registerSpellEffectEvent(ABILITY_ID_BEAR_FORM_DUMMY, () -> onCast(FORM_IDS[1]))
    registerSpellEffectEvent(ABILITY_ID_PANTHER_FORM_DUMMY, () -> onCast(FORM_IDS[2]))
    registerSpellEffectEvent(ABILITY_ID_TIGER_FORM_DUMMY, () -> onCast(FORM_IDS[3]))

    //Initialize form map
    var i = 0
    for oldForm = 0 to 3
        for newForm = 0 to 3
            if newForm != oldForm
                let abiKey = FORM_IDS[oldForm].toRawCode() + FORM_IDS[newForm].toRawCode()
                FORM_MAP.put(abiKey, TRANSFORM_ABILITY_IDS[i])
                i++
