package FiretrapDemo
import ClosureTimers
import ClosureEvents
import ClosureForGroups

rect elk_run = Rect(2880, -11264, 2944, -11200)
rect fire_location = Rect(2816, -11136, 2944, -11008)
rect block_location = Rect(2688, -11264, 2816, -11136)
rect gatherer_spawn = Rect(2144, -11328, 2272, -11200)

function runFireTrapDemo()
    unit elk = null
    unit gatherer = null
    texttag text = null
    CallbackPeriodic updaterLoop = null

    elk = CreateUnit(playerFromIndex(PLAYER_NEUTRAL_AGGRESSIVE), UNIT_ELK, elk_run.getCenterX(), elk_run.getCenterY(), 0)
    gatherer = CreateUnit(playerFromIndex(PLAYER_NEUTRAL_PASSIVE), UNIT_GATHERER, gatherer_spawn.getCenterX(), gatherer_spawn.getCenterY(), 0)
    let fire = gatherer.addItemById(ITEM_FIRE_KIT)
    gatherer.useItemPoint(fire, fire_location.getCenter())

    //Create tip text
    text = CreateTextTagUnitBJ("TIP:\nelks can be trapped with a campfire!", gatherer, 40, 9, 0, 255, 0, 0)
    text.setVisibility(true)
    text.setLifespan(10)

    //Keep elk from escaping with random wander, update text pos
    updaterLoop = doPeriodically(0.05) cb ->
        elk.issuePointOrder("move", elk_run.getCenter())
        text.setPos(gatherer.getPos().withTerrainZ(40))

    //Wait for fire to build
    doAfter(2.2) ->
        gatherer.issuePointOrder("move", block_location.getCenter())
        doAfter(0.6) ->
            gatherer.issueTargetOrder("attack", elk)
            destroy updaterLoop
            doAfter(8) ->
                elk.remove()
                gatherer.remove()
                text.destr()
                elk = null
                gatherer = null
                text = null
                updaterLoop = null
                forUnitsInRect(gg_rct_vision) u ->
                    if u.getTypeId() == UNIT_FIRE
                        u.remove()

init
    //Run demo twice
    runFireTrapDemo()
    doAfter(16) ->
        runFireTrapDemo()
